buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'java'

build_number = System.getenv().BUILD_NUMBER
String semver = "${version_major}.${version_minor}.${version_patch}"
version = "${semver}.${build_number}"

group = project.jarGroup
archivesBaseName = "${project.archiveBase}-${project.MCVersion}-${version}"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "${project.MCPMappings}-${project.MCVersion}"
	
    runs {
        client = {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
        server = {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }
    }
}

repositories {
}

dependencies {
	minecraft "net.minecraftforge:forge:${project.MCVersion}-${project.ForgeVersion}"
}

processResources {
	inputs.property "version", version
	inputs.property "mcversion", project.MCVersion
	from(sourceSets.main.resources.srcDirs) {
		include 'mods.toml'
		expand 'version': version, 'mcversion': project.MCVersion
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mods.toml'
	}
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
	archives sourcesJar
	archives deobfJar
}

jar {
    manifest {
        attributes(["Specification-Title": "Quick Leaf Decay",
                    "Specification-Vendor": "Lumien, JayJay1989BE",
                    "Specification-Version": "${semver}",
                    "Implementation-Title": "quickleafdecay",
                    "Implementation-Version": "${project.version}",
                    "Implementation-Vendor" : "Lumien, JayJay1989BE",
					"Original-Author" : "Lumien",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }
	if (project.hasProperty("signSHA1")) {
		manifest.attributes += ["Fingerprint": project.getProperty("signSHA1")]
	}
}
